// this e2e test must prove that this issue is fixed:
// https://github.com/nevalang/neva/issues/575
// all elements of the array must be printed befofe program terminate

import { time }

const lst list<int> = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]

def Main(start) (stop) {
    ListToStream<int>
    Map<int, int>{Slow}
    List
    Println
    ---
    :start -> {
        $lst -> listToStream -> map -> list -> println -> :stop
    }
}

def Slow(data int) (res int) {
    time.Delay<int>
    Dec<int>
    ---
    :data -> delay:data
    $time.second -> delay:dur
    delay -> dec -> :res
}

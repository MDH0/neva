def Main(start) (stop) {
	For<T>{Next2Lines}, Panic
	---
	:start -> 99..-1 -> for
	for:res -> wait -> :stop
	for:err -> panic
}

def Next2Lines(data int) (sig any, err error) {
	first Relay<int>{FirstLine}?
	dec Dec<int>
	second SecondLine?
	---
	:data -> first -> dec -> second -> :res
}

def FirstLine(n int) (sig any, err error) {
	p1 Println, p2 Println, p3 Printf?
	---
	:n -> switch {
		0 -> { 'No more bottles of beer on the wall, no more bottles of beer.' -> p1 }
		1 -> { '1 bottle of beer on the wall, 1 bottle of beer.' -> p2 }
		else -> [
			p3:args[0],
			{ '$0 bottles of beer on the wall, $0 bottles of beer.\n' -> p3:tpl }
		]
	}
	[p1, p2, p3] -> :sig
}

def SecondLine(n int) (sig any, err error) {
	p1 Println, p2 Println, p3 Println, p4 Printf?
	---
	:n -> switch {
		-1 -> { 'Go to the store and buy some more, 99 bottles of beer on the wall.' -> p1 }
		0 -> { 'Take one down and pass it around, no more bottles of beer on the wall.\n' -> p2 } 
		1 -> { 'Take one down and pass it around, 1 bottle of beer on the wall.\n' -> p3 }
		else -> [
			p4:args[0],
			{ 'Take one down and pass it around, $0 bottles of beer on the wall.\n\n' -> p4:tpl }
		]
	}
	[p1, p2, p3, p4] -> :sig
}

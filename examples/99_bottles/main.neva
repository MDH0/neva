def Main(start) (stop) {
	App, Panic
	---
	:start -> app
	app:err -> panic
	app:sig -> :stop
}

def App(sig any) (sig any, err error) {
	first For<int>{FirstLine}?
	dec Dec<int>
	second For<int>{SecondLine}?
	wait Wait
	---
	:sig -> 99..-1 -> first -> dec -> second -> wait -> :sig
}

def FirstLine(data int) (sig any, err error) {
	p1 Println, p2 Println, p3 Printf?
	---
	:data -> switch {
		0 -> { 'No more bottles of beer on the wall, no more bottles of beer.' -> p1 }
		1 -> { '1 bottle of beer on the wall, 1 bottle of beer.' -> p2 }
		else -> [
			p3:args[0],
			{ '$0 bottles of beer on the wall, $0 bottles of beer.\n' -> p3:tpl }
		]
	}
	[p1, p2, p3] -> :sig
}

def SecondLine(data int) (sig any, err error) {
	p1 Println, p2 Println, p3 Println, p4 Printf?
	---
	:data -> switch {
		-1 -> { 'Go to the store and buy some more, 99 bottles of beer on the wall.' -> p1 }
		0 -> { 'Take one down and pass it around, no more bottles of beer on the wall.\n' -> p2 } 
		1 -> { 'Take one down and pass it around, 1 bottle of beer on the wall.\n' -> p3 }
		else -> [
			p4:args[0],
			{ 'Take one down and pass it around, $0 bottles of beer on the wall.\n\n' -> p4:tpl }
		]
	}
	[p1, p2, p3, p4] -> :sig
}
